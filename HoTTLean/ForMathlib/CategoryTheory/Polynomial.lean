/-
Copyright (c) 2025 Joseph Hua. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Hua, Sina Hazratpour, Emily Riehl
-/

import Mathlib.CategoryTheory.MorphismProperty.OverAdjunction
import Mathlib.CategoryTheory.Functor.TwoSquare
import Mathlib.CategoryTheory.NatTrans.IsCartesian
import Mathlib.CategoryTheory.Comma.Over.Pushforward
import HoTTLean.ForMathlib

universe v u v‚ÇÅ u‚ÇÅ

noncomputable section

namespace CategoryTheory

open Category Limits MorphismProperty

variable {C : Type u} [Category.{v} C]

namespace MorphismProperty

namespace PolynomialPartialAdjunction

variable {T : Type u} [Category.{v} T] {R : MorphismProperty T}
  [R.HasPullbacks] [R.IsStableUnderBaseChange]
  {Q : MorphismProperty T} [Q.HasPullbacks] [R.HasPushforwards Q]
  [R.IsStableUnderPushforward Q]
  {E I B : T} (i : E ‚ü∂ I) (p : E ‚ü∂(Q) B)

/-- The partial right adjoint representing a multivariate polynomial. -/
abbrev partialRightAdjoint := Over.pullback R ‚ä§ i ‚ãô pushforward R p

/-- The left adjoint in the partial adjunction. -/
abbrev leftAdjoint := CategoryTheory.Over.pullback p.1 ‚ãô CategoryTheory.Over.map i

/-- `pullback R ‚ä§ i ‚ãô pushforward R p` is a partial right adjoint to
`CategoryTheory.Over.pullback p.1 ‚ãô CategoryTheory.Over.map i`
  ```
         pullback i       pushforward p
   R.Over I ------> R.Over E -----> R.Over B
      |               |                |
      |       ‚ä•       |        ‚ä•       |
      |               |                |
      V               V                V
     C/I <--------- C/E <------------ C/B
            map i         pullback p
  ```

On paper this is written `C/B (X, p‚Åé (i* Y)) ‚âÉ C/I (i! (p* X), Y)`.
-/
def homEquiv {X : Over B} {Y : R.Over ‚ä§ I} :
    (X ‚ü∂ ((partialRightAdjoint i p).obj Y).toComma) ‚âÉ
    ((leftAdjoint i p).obj X ‚ü∂ Y.toComma) :=
  calc (X ‚ü∂ ((R.pushforward p).obj ((Over.pullback R ‚ä§ i).obj Y)).toComma)
  _ ‚âÉ ((CategoryTheory.Over.pullback p.1).obj X ‚ü∂ ((Over.pullback R ‚ä§ i).obj Y).toComma) :=
    pushforward.homEquiv ..
  _ ‚âÉ ((CategoryTheory.Over.map i).obj
      ((CategoryTheory.Over.pullback p.fst).obj X) ‚ü∂ Y.toComma) :=
    pullback.homEquiv ..

lemma homEquiv_comp {X X' : Over B} {Y : R.Over ‚ä§ I}
    (f : X' ‚ü∂ ((partialRightAdjoint i p).obj Y).toComma) (g : X ‚ü∂ X') :
    homEquiv i p (g ‚â´ f) =
    (leftAdjoint i p).map g ‚â´ homEquiv i p f := by
  unfold homEquiv
  simp only [Functor.comp_obj, Equiv.trans_def, Equiv.trans_apply]
  erw [pushforward.homEquiv_comp, pullback.homEquiv_comp]
  rfl

lemma homEquiv_map_comp {X : Over B} {Y Y' : R.Over ‚ä§ I}
    (f : X ‚ü∂ ((partialRightAdjoint i p).obj Y).toComma) (g : Y ‚ü∂ Y') :
    homEquiv i p (f ‚â´ Comma.Hom.hom ((partialRightAdjoint i p).map g)) =
    homEquiv i p f ‚â´ Comma.Hom.hom g := by
  unfold homEquiv
  simp only [Functor.comp_obj, Equiv.trans_def, Equiv.trans_apply]
  erw [pushforward.homEquiv_map_comp, pullback.homEquiv_map_comp]
  rfl

lemma homEquiv_symm_comp {X : Over B} {Y Y' : R.Over ‚ä§ I}
    (f : (leftAdjoint i p).obj X ‚ü∂ Y.toComma) (g : Y ‚ü∂ Y') :
    (homEquiv i p).symm f ‚â´ Comma.Hom.hom ((partialRightAdjoint i p).map g) =
    (homEquiv i p).symm (f ‚â´ Comma.Hom.hom g) := by
  unfold homEquiv
  simp
  erw [pushforward.homEquiv_symm_comp, pullback.homEquiv_symm_comp]
  rfl

lemma homEquiv_comp_symm {X X' : Over B} {Y : R.Over ‚ä§ I}
    (f : (leftAdjoint i p).obj X' ‚ü∂ Y.toComma) (g : X ‚ü∂ X') :
    g ‚â´ (homEquiv i p).symm f =
    (homEquiv i p).symm ((leftAdjoint i p).map g ‚â´ f) := by
  unfold homEquiv
  simp
  erw [pushforward.homEquiv_comp_symm, pullback.homEquiv_comp_symm]
  rfl

/-- The counit of the partial adjunction is given by evaluating the equivalence of
hom-sets at the identity.
On paper we write this as `counit : i! p* p‚àó i* => Over.forget : R.Over ‚ä§ I ‚•§ Over I`
-/
def counit :
    partialRightAdjoint i p ‚ãô Over.forget R ‚ä§ B ‚ãô leftAdjoint i p ‚ü∂ Over.forget R ‚ä§ I where
  app _ := homEquiv i p (ùüô _)
  naturality X Y f := by
    apply (homEquiv i p).symm.injective
    conv => left; erw [‚Üê homEquiv_comp_symm]
    conv => right; erw [‚Üê homEquiv_symm_comp]
    simp

end PolynomialPartialAdjunction

variable (P : MorphismProperty C)

namespace Over

@[simps]
def equivalenceOfHasObjects' (R : MorphismProperty C) [R.HasObjects]
    {X : C} (hX : IsTerminal X) : R.Over ‚ä§ X ‚âå Over X where
  functor := MorphismProperty.Over.forget _ _ _
  inverse := Comma.lift (ùü≠ _) (by intro; apply HasObjects.obj_mem _ hX) (by simp) (by simp)
  unitIso := eqToIso rfl
  counitIso := eqToIso rfl
  functor_unitIso_comp := by simp

@[simp]
def equivalenceOfHasObjects (R : MorphismProperty C) [R.HasObjects]
    {X : C} (hX : IsTerminal X) : R.Over ‚ä§ X ‚âå C :=
  (equivalenceOfHasObjects' R hX).trans (Over.equivalenceOfIsTerminal hX)

variable {P : MorphismProperty C} {E B : C}

@[simps]
def ofMorphismProperty (p : E ‚ü∂(P) B) : P.Over ‚ä§ B where
  left := E
  right := ‚ü®‚ü®‚ü©‚ü©
  hom := p.1
  prop := p.2

@[simps]
def homMkTop {p q : P.Over ‚ä§ B} (left : p.left ‚ü∂ q.left) (hleft : left ‚â´ q.hom = p.hom) :
    p ‚ü∂ q where
  left := left
  right := eqToHom (by simp)
  w := by simp [hleft]
  prop_hom_left := trivial
  prop_hom_right := trivial

/--
Convert an object `p` in `R.Over ‚ä§ B` to a morphism in `R.Over ‚ä§ O` by composing with `o`.
     p
 E -----> B
  \      /
   \    /o
    \  /
     VV
     O
-/
@[simp]
def homOfMorphismProperty [P.IsStableUnderComposition] {O} (p : P.Over ‚ä§ B) (o : B ‚ü∂(P) O) :
    (map ‚ä§ o.2).obj p ‚ü∂ Over.ofMorphismProperty o :=
  Over.homMk p.hom (by simp)

end Over

end MorphismProperty

open MorphismProperty.Over

/-- `P : MvPoly R H I O` is a the signature for a multivariate polynomial functor,
consisting of the following maps
```
         p
      E ---> B
  i ‚Üô         ‚Üò o
  I               O
```
We can lazily read this as `‚àë b : B, X ^ (E b)`,
for some `X` in the (`P`-restricted) slice over `I`.

In full detail:
Viewing such an `X` as a series of variables `X_k` indexed by `k ‚àà I`,
and `B` as a family of types `B_k` indexed by `j ‚àà O`
this can be further viewed as `O`-many `I`-ary polynomials `‚àë b : B_j, X_(i b) ^ (E b)`

To explain the need for two morphism properties,
consider the following two use-cases:
1. `R = ‚ä§` is all maps and the category has all pullbacks.
  `H` is the class of exponentiable maps - it follows from all maps having pullbacks that `H`
  also has pullbacks.
2. `R = H` is a œÄ-clan, [see Joyal, def 2.4.1](https://arxiv.org/pdf/1710.10238).

This will typically be used with the following instances

- For pullback of `R`-maps along `i`, `p` and `o` we need
  `[R.IsStableUnderBaseChange] [R.HasPullbacks]`
- For the left adjoint to pullback for `o` we need `[R.IsStableUnderComposition]`
- For pushforward of `R`-maps along `p` we need
  `[R.IsStableUnderPushforward H] [R.HasPushforwards H]`
- For pushforward of `R`-maps along `p` we also assume `[H.HasPullbacks]`.
  This is useful - it makes the `R`-restricted pushforward of `R`-maps along `p`
  a partial left adjoint to *global* pullback along `p`,
  ```
        pushforward p
   R.Over E -----> R.Over B
      |              |
      |       ‚ä•      |
      |              |
      V              V
     C/E <--------- C/B
         pullback p
  ```
  which is strictly stronger than just having a left adjoint to `R`-restricted pullback
  `(pullback : R.Over B ‚•§ R.Over E) ‚ä£ (pushforward : R.Over E ‚•§ R.Over B)`.
-/
structure MvPoly (R : MorphismProperty C) (H : MorphismProperty C) (I O : C) where
  (E B : C)
  (i : E ‚ü∂(R) I)
  (p : E ‚ü∂(H) B)
  (o : B ‚ü∂(R) O)

namespace MvPoly

instance : (‚ä§ : MorphismProperty C).HasOfPostcompProperty ‚ä§ where
  of_postcomp := by simp

variable {R : MorphismProperty C} {H : MorphismProperty C}

instance {B O : C} (i : B ‚ü∂(R) O) [R.HasPullbacks] [R.IsStableUnderBaseChange]
    [R.IsStableUnderComposition] : (pullback R ‚ä§ i.1).IsRightAdjoint :=
  (mapPullbackAdj R ‚ä§ i.1 i.2 ‚ü®‚ü©).isRightAdjoint

variable {I O : C} (P : MvPoly R H I O) [R.HasPullbacks] [R.IsStableUnderBaseChange]
    [H.HasPullbacks] [R.HasPushforwards H]
    [R.IsStableUnderPushforward H]

open PolynomialPartialAdjunction

/-- (Ignoring the indexing from `i` and `o`)
This is the first projection morphism from `P @ X = ‚àë b : B, X ^ (E b)` to `B`,
as an object in the `P`-restricted slice over `B`. -/
abbrev fstProj (P : MvPoly R H I O) (X : R.Over ‚ä§ I) : R.Over ‚ä§ P.B :=
  (partialRightAdjoint P.i.1 P.p).obj X

/-- The counit of the adjunction `pullback p ‚ãô map i ‚ä£ pullback i ‚ãô pushforward p` evaluated at `X`.
Ignoring the indexing from `i` and `o`,
this can be viewed as the second projection morphism from `P @ X = ‚àë b : B, X ^ (E b)`
to `X^ (E b)`.

```
     X ----------> I
     ‚àß             ‚àß
     |             |
 sndProj           | i
     |             |
     ‚Ä¢ ----------> E
     |             |
     |    (pb)     |
     |             |p
     V    fstProj  V
   P @ X --------> B
       ‚üç          |
          ‚üç       |o
             ‚üç    |
                ‚Üò V
                   O
```
-/
def sndProj (P : MvPoly R H I O) (X : R.Over ‚ä§ I) :
    (leftAdjoint P.i.1 P.p).obj (fstProj P X).toComma ‚ü∂ X.toComma :=
  (counit P.i.1 P.p).app X

section

variable (P : MvPoly R H I O) {X Y : R.Over ‚ä§ I} (f : X ‚ü∂ Y)

@[reassoc (attr := simp)]
lemma map_fstProj :
    ((partialRightAdjoint P.i.1 P.p).map f).left ‚â´ (fstProj P Y).hom = (fstProj P X).hom := by
  simp

lemma sndProj_comp_hom : (sndProj P X).left ‚â´ X.hom = pullback.snd _ _ ‚â´ P.i.1 := by
  simp [sndProj]

lemma sndProj_comp : (sndProj P X).left ‚â´ f.left =
    pullback.map _ _ _ _
      ((partialRightAdjoint P.i.1 P.p).map f).left (ùüô _) (ùüô _) (by simp) (by simp) ‚â´
      (sndProj P Y).left := by
  have := congr_arg CommaMorphism.left <| (counit P.i.1 P.p).naturality f
  simpa [pullback.map] using this.symm

end

variable [R.IsStableUnderComposition]
/-- A multivariate polynomial signature
```
         p
      E ---> B
  i ‚Üô         ‚Üò o
  I               O
```
gives rise to a functor
```
                         pushforward p
                R.Over ‚ä§ E ---------> R.Over ‚ä§ B
     pullback i ‚Üó                              ‚üç map o
             ‚üã                                    ‚üç
          ‚üã                                          ‚Üò
  R.Over ‚ä§ I                                      R.Over ‚ä§ O
```
-/
def functor : R.Over ‚ä§ I ‚•§ R.Over ‚ä§ O :=
  pullback R ‚ä§ P.i.1 ‚ãô MorphismProperty.pushforward R P.p ‚ãô map ‚ä§ P.o.2

/-- The action of a univariate polynomial on objects. -/
def apply (P : MvPoly R H I O) : R.Over ‚ä§ I ‚Üí R.Over ‚ä§ O := (functor P).obj

@[inherit_doc]
infix:90 " @ " => apply

namespace Equiv

variable {P : MvPoly R H I O} {Œì : Over O} {X : R.Over ‚ä§ I}

def fst (pair : Œì ‚ü∂ (P @ X).toComma) : Over P.B := Over.mk (pair.left ‚â´ (fstProj P X).hom)

abbrev sndDom (pair : Œì ‚ü∂ (P @ X).toComma) : Over I := (leftAdjoint P.i.1 P.p).obj (fst pair)

def snd (pair : Œì ‚ü∂ (P @ X).toComma) : sndDom pair ‚ü∂ X.toComma :=
  homEquiv P.i.1 P.p (Over.homMk (pair.left))

lemma snd_eq (pair : Œì ‚ü∂ (P @ X).toComma) : snd pair =
    (leftAdjoint P.i.1 P.p).map (Over.homMk (pair.left)) ‚â´ sndProj P X := by
  erw [Equiv.apply_eq_iff_eq_symm_apply, ‚Üê homEquiv_comp_symm]
  simp [sndProj, counit]

def mk (f : Over P.B) (hf : Œì = (Over.map P.o.1).obj f)
    (s : (leftAdjoint P.i.1 P.p).obj f ‚ü∂ X.toComma) :
    Œì ‚ü∂ (P @ X).toComma :=
  eqToHom hf ‚â´ (Over.map P.o.fst).map ((homEquiv P.i.1 P.p).symm s)

@[simp]
lemma fst_mk (f : Over P.B) (hf : Œì = (Over.map P.o.1).obj f)
    (s : (leftAdjoint P.i.1 P.p).obj f ‚ü∂ X.toComma) : fst (mk f hf s) = f := by
  subst hf; simp [fst, mk]; rfl

lemma snd_mk (f : Over P.B) (hf : Œì = (Over.map P.o.1).obj f)
    (s : (leftAdjoint P.i.1 P.p).obj f ‚ü∂ X.toComma) : snd (mk f hf s) =
    eqToHom (by simp) ‚â´ s := calc snd (mk f hf s)
  _ = (leftAdjoint P.i.1 P.p).map (eqToHom (fst_mk f hf s)) ‚â´ s := by
    erw [Equiv.apply_eq_iff_eq_symm_apply, ‚Üê homEquiv_comp_symm]
    ext
    simp [mk]
  _ = eqToHom _ ‚â´ s := by
    simp only [eqToHom_map]

@[simp]
lemma map_fst (pair : Œì ‚ü∂ (P @ X).toComma) : (Over.map P.o.fst).obj (fst pair) = Œì := by
  have := pair.w
  simp only [Functor.id_obj, Functor.const_obj_obj, Functor.id_map,
    CostructuredArrow.right_eq_id, Functor.const_obj_map, comp_id] at this
  simp [Over.map, Comma.mapRight, fst]
  congr

@[simp]
lemma eta (pair : Œì ‚ü∂ (P @ X).toComma) : mk (fst pair) (by simp) (snd pair) = pair := by
  ext
  simp [mk, snd]

end Equiv

instance (P : MvPoly R H I O) : Limits.PreservesLimitsOfShape WalkingCospan
    (MorphismProperty.Over.map ‚ä§ P.o.2) := by sorry

instance (P : MvPoly R H I O) :
    Limits.PreservesLimitsOfShape WalkingCospan (MvPoly.functor P) := by
  dsimp [functor]
  have : (MorphismProperty.Over.pullback R ‚ä§ P.i.1).IsRightAdjoint :=
    Adjunction.isRightAdjoint (MorphismProperty.Over.mapPullbackAdj R ‚ä§ P.i.1 P.i.2 trivial)
  infer_instance

end MvPoly

/-- `P : UvPoly R E B` is the type of signatures for polynomial functors
         p
      E ---> B

We read this as `‚àë b : B, X ^ (E b)`,
for some `R`-object `X` (meaning the unique map to the terminal object is in `R`).

This notion of polynomial makes sense when `R` is a œÄ-clan,
[see Joyal, def 2.4.1](https://arxiv.org/pdf/1710.10238).
Therefore it will typically be used with the following instances

- For pullback of `R`-maps along `p` we need
  `[R.IsStableUnderBaseChange] [R.HasPullbacks]`
- For the left adjoint to pullback along `B`, we assume `[R.IsStableUnderComposition]`
  and `[R.HasObjects]`, meaning the unique map `B ‚ü∂ ‚ä§_ C` is in `R`.
  For this, we will also assume `[HasTerminal C]`.
- For pushforward of `R`-maps along `p` we need
  `[R.IsStableUnderPushforward R] [R.HasPushforwards R]`
- For pushforward of `R`-maps along `p` we also assume `[R.HasPullbacks]`.
  This is useful - it makes the `R`-restricted pushforward of `R`-maps along `p`
  a partial left adjoint to *global* pullback along `p`,
  ```
        pushforward p
   R.Over E -----> R.Over B
      |              |
      |       ‚ä•      |
      |              |
      V              V
     C/E <--------- C/B
         pullback p
  ```
  which is strictly stronger than just having a left adjoint to `R`-restricted pullback
  `(pullback : R.Over B ‚•§ R.Over E) ‚ä£ (pushforward : R.Over E ‚•§ R.Over B)`.
-/
structure UvPoly (R : MorphismProperty C) (E B : C) where
  (p : E ‚ü∂ B)
  (morphismProperty : R p)

namespace UvPoly

section

variable {R : MorphismProperty C} {E B : C}

variable [HasTerminal C]

variable [R.IsStableUnderComposition] [R.HasPullbacks] [R.IsStableUnderBaseChange] [R.HasObjects]
  [R.IsStableUnderPushforward R] [R.HasPushforwards R]

instance (P : UvPoly R E B) {Œì : C} (A : Œì ‚ü∂ B) : HasPullback A P.p := by
  let p : E ‚ü∂(R) B := ‚ü® P.p, P.morphismProperty ‚ü©
  convert_to HasPullback A p.1
  apply MorphismProperty.instHasPullbackFstHomOfHasPullbacks

instance (P : UvPoly R E B) {Œì : C} (A : Œì ‚ü∂ B) : HasPullback P.p A :=
  hasPullback_symmetry _ _

def object (X : C) : X ‚ü∂(R) ‚ä§_ C :=
  ‚ü®terminal.from X, HasObjects.obj_mem _ terminalIsTerminal‚ü©

@[simp]
abbrev toOverTerminal : C ‚•§ R.Over ‚ä§ (‚ä§_ C) :=
  (equivalenceOfHasObjects R terminalIsTerminal).inverse

@[simp]
abbrev fromOverTerminal : R.Over ‚ä§ (‚ä§_ C) ‚•§ C :=
  (equivalenceOfHasObjects R terminalIsTerminal).functor

@[simps]
def mvPoly (P : UvPoly R E B) : MvPoly R R (‚ä§_ C) (‚ä§_ C) where
  E := E
  B := B
  i := object E
  p := ‚ü®P.p, P.morphismProperty‚ü©
  o := object B

def functor (P : UvPoly R E B) : C ‚•§ C :=
  toOverTerminal ‚ãô
  MvPoly.functor P.mvPoly ‚ãô
  fromOverTerminal

/-- The action of a univariate polynomial on objects. -/
def apply [HasTerminal C] (P : UvPoly R E B) : C ‚Üí C := P.functor.obj

@[inherit_doc]
infix:90 " @ " => apply

instance [HasTerminal C] (P : UvPoly R E B) :
    Limits.PreservesLimitsOfShape WalkingCospan P.functor := by
  unfold functor
  infer_instance

variable (B)

/-- The identity polynomial functor in single variable. -/
@[simps!]
def id (R : MorphismProperty C) [R.ContainsIdentities] (B) : UvPoly R B B := ‚ü®ùüô B, R.id_mem _ ‚ü©

@[simps!]
def vcomp [R.IsStableUnderComposition] {A B C} (P : UvPoly R A B) (Q : UvPoly R B C) :
    UvPoly R A C :=
  ‚ü® P.p ‚â´ Q.p, R.comp_mem _ _ P.morphismProperty Q.morphismProperty ‚ü©

variable {B}

/-- The fstProjection morphism from `‚àë b : B, X ^ (E b)` to `B` again. -/
def fstProj (P : UvPoly R E B) (X : C) : P @ X ‚ü∂ B :=
  (P.mvPoly.fstProj (toOverTerminal.obj X)).hom

@[reassoc (attr := simp)]
lemma map_fstProj (P : UvPoly R E B) {X Y : C} (f : X ‚ü∂ Y) :
    P.functor.map f ‚â´ fstProj P Y = fstProj P X :=
  P.mvPoly.map_fstProj (toOverTerminal.map f)

/-- The second projection morphism from `P @ X = ‚àë b : B, X ^ (E b)` to `X^ (E b)`. -/
def sndProj (P : UvPoly R E B) (X : C) :
    Limits.pullback (fstProj P X) P.p ‚ü∂ X :=
  (P.mvPoly.sndProj (toOverTerminal.obj X)).left

lemma sndProj_comp (P : UvPoly R E B) {X Y : C} (f : X ‚ü∂ Y) :
    sndProj P X ‚â´ f =
    pullback.map _ _ _ _ (P.functor.map f) (ùüô _) (ùüô _) (by simp) (by simp) ‚â´ sndProj P Y :=
  P.mvPoly.sndProj_comp (toOverTerminal.map f)

open TwoSquare

/-- A vertical map `œÅ : P.p.1 ‚ü∂ Q.p.1` of polynomials (i.e. a commutative triangle)
```
    œÅ
E ----> F
 \     /
  \   / \ /
    B
```
induces a natural transformation `Q.functor ‚ü∂ P.functor ` obtained by pasting the following 2-cells
```
              Q.p.1
C --- >  C/F ----> C/B -----> C
|         |          |        |
|   ‚Üô     | œÅ*  ‚âÖ    |   =    |
|         v          v        |
C --- >  C/E ---->  C/B ----> C
              P.p.1
```
-/
def verticalNatTrans {F : C} (P : UvPoly R E B) (Q : UvPoly R F B) (œÅ : E ‚ü∂ F)
    (h : P.p = œÅ ‚â´ Q.p) : Q.functor ‚ü∂ P.functor := sorry --by
  -- have sq : CommSq œÅ P.p.1 Q.p.1 (ùüô _) := by simp [h]
  -- let cellLeft := (Over.starPullbackIsoStar œÅ).hom
  -- let cellMid := (pushforwardPullbackTwoSquare œÅ P.p Q.p (ùüô _) sq)
  -- let cellLeftMidPasted := TwoSquare.whiskerRight (cellLeft ‚â´‚Çï cellMid) (Over.pullbackId).inv
  -- simpa using (cellLeftMidPasted ‚â´‚Çï (vId (forget B)))

/-- A cartesian map of polynomials
```
           P.p
      E -------->  B
      |            |
   œÜ  |            | Œ¥
      v            v
      F -------->  D
           Q.p
```
induces a natural transformation between their associated functors obtained by pasting the following
2-cells
```
              Q.p
C --- >  C/F ----> C/D -----> C
|         |          |        |
|   ‚Üó     | œÜ*  ‚âÖ    | Œ¥* ‚Üó   |
|         v          v        |
C --- >  C/E ---->  C/B ----> C
              P.p
```
-/
def cartesianNatTrans {D F : C} (P : UvPoly R E B) (Q : UvPoly R F D)
    (Œ¥ : B ‚ü∂ D) (œÜ : E ‚ü∂ F) (pb : IsPullback P.p œÜ Œ¥ Q.p) : P.functor ‚ü∂ Q.functor :=
  sorry
  -- let cellLeft : TwoSquare (ùü≠ C) (Over.star F) (Over.star E) (pullback œÜ) :=
  --   (Over.starPullbackIsoStar œÜ).inv
  -- let cellMid :  TwoSquare (pullback œÜ) (pushforward Q.p) (pushforward P.p) (pullback Œ¥) :=
  --   (pushforwardPullbackIsoSquare pb.flip).inv
  -- let cellRight : TwoSquare (pullback Œ¥) (forget D) (forget B) (ùü≠ C) :=
  --   pullbackForgetTwoSquare Œ¥
  -- let := cellLeft ‚â´·µ• cellMid ‚â´·µ• cellRight
  -- this

theorem isCartesian_cartesianNatTrans {D F : C} (P : UvPoly R E B) (Q : UvPoly R F D)
    (Œ¥ : B ‚ü∂ D) (œÜ : E ‚ü∂ F) (pb : IsPullback P.p œÜ Œ¥ Q.p) :
    (cartesianNatTrans P Q Œ¥ œÜ pb).IsCartesian := by
  sorry
  -- simp [cartesianNatTrans]
  -- infer_instance

  -- (isCartesian_of_isIso _).vComp <|
  -- (isCartesian_of_isIso _).vComp <|
  -- isCartesian_pullbackForgetTwoSquare _

/-- A morphism from a polynomial `P` to a polynomial `Q` is a pair of morphisms `e : E ‚ü∂ E'`
and `b : B ‚ü∂ B'` such that the diagram
```
      E -- P.p ->  B
      ^            |
   œÅ  |            |
      |     œà      |
      Pb --------> B
      |            |
   œÜ  |            | Œ¥
      v            v
      F -- Q.p ->  D
```
is a pullback square. -/
structure Hom {F D : C} (P : UvPoly R E B) (Q : UvPoly R F D) where
  Pb : C
  Œ¥ : B ‚ü∂ D
  œÜ : Pb ‚ü∂ F
  œà : Pb ‚ü∂ B
  œÅ : Pb ‚ü∂ E
  is_pb : IsPullback œà œÜ Œ¥ Q.p
  w : œÅ ‚â´ P.p = œà

namespace Hom

open IsPullback

/-- The identity morphism in the category of polynomials. -/
def id (P : UvPoly R E B) : Hom P P := ‚ü®E, ùüô B, ùüô _ , P.p , ùüô _, IsPullback.of_id_snd, by simp‚ü©

end Hom

/-- The domain of the composition of two polynomial signatures.
See `UvPoly.comp`. -/
def compDom {E B E' B' : C} (P : UvPoly R E B) (P' : UvPoly R E' B') : C :=
  Limits.pullback (sndProj P B') P'.p

/--
The composition of two polynomial signatures. See `UvPoly.comp`.
Note that this is not just composition in the category `C`,
instead it is functor composition in the category `C ‚•§ C`,
meaning it satisfies `P.functor ‚ãô P'.functor ‚âÖ (comp P P').functor`.

   E' <---- compDom
   |           |
p' |   (pb)    |
   |           |
   V           V
   B' <-----   ‚Ä¢ -------> E
      sndProj  |          |
               |   (pb)   |p
               |          |
               V          V
             P @ B' -----> B
                    fstProj
-/
@[simps!]
def comp {E B E' B' : C} (P : UvPoly R E B) (P' : UvPoly R E' B') :
    UvPoly R (compDom P P') (P @ B') where
  p := Limits.pullback.fst (sndProj P B') P'.p ‚â´ pullback.fst (fstProj P B') P.p
  morphismProperty := R.comp_mem _ _
   (R.of_isPullback (IsPullback.of_hasPullback (sndProj P B') P'.p).flip P'.morphismProperty)
   (R.of_isPullback (IsPullback.of_hasPullback (fstProj P B') P.p).flip P.morphismProperty)

namespace Equiv

variable {P : UvPoly R E B} {Œì X Y : C}

/-- Convert the morphism `pair` into a morphism in the over category `Over (‚ä§_ C)` -/
@[simp]
abbrev homMk (pair : Œì ‚ü∂ P @ X) : Over.mk (terminal.from Œì) ‚ü∂
    ((toOverTerminal ‚ãô MvPoly.functor P.mvPoly).obj X).toComma := Over.homMk pair

/--
A morphism `pair : Œì ‚ü∂ P @ X` is equivalent to a pair of morphisms
`fst : Œì ‚ü∂ B` and `snd : pb ‚ü∂ X` in the following diagram
```
    snd
B <---- pb ------> E
        |          |
        |          |p
        |          |
        V          V
        Œì -------> B
             fst
```
The following API allows users to convert back and forth along this (natural) bijection.
-/
def fst (pair : Œì ‚ü∂ P @ X) : Œì ‚ü∂ B :=
  (MvPoly.Equiv.fst (homMk pair)).hom

lemma fst_eq (pair : Œì ‚ü∂ P @ X) : fst pair = pair ‚â´ P.fstProj X := by
  aesop_cat

def snd (pair : Œì ‚ü∂ P @ X) : Limits.pullback (fst pair) P.p ‚ü∂ X :=
  (MvPoly.Equiv.snd (homMk pair)).left

lemma snd_eq (pair : Œì ‚ü∂ P @ X) : snd pair =
    Limits.pullback.map (fst pair) P.p (P.fstProj X) P.p pair (ùüô E) (ùüô B) (by simp [fst_eq])
    (by simp) ‚â´ sndProj P X := by
  simpa [Limits.pullback.map] using congrArg CommaMorphism.left (MvPoly.Equiv.snd_eq (homMk pair))

def snd' (pair : Œì ‚ü∂ P @ X) {pb f g} (H : IsPullback (P := pb) f g (fst pair) P.p) : pb ‚ü∂ X :=
  H.isoPullback.hom ‚â´ snd pair

theorem snd_eq_snd' (pair : Œì ‚ü∂ P @ X) : snd pair = snd' pair (.of_hasPullback ..) :=
  by simp [snd']

/-- Convert the morphism `x` into a morphism in the over category `Over (‚ä§_ C)` -/
@[simp]
abbrev mkAux (b : Œì ‚ü∂ B) (x : pullback b P.p ‚ü∂ X) :
    (PolynomialPartialAdjunction.leftAdjoint P.mvPoly.i.fst P.mvPoly.p).obj (Over.mk b) ‚ü∂
    ((toOverTerminal (R := R)).obj X).toComma :=
  Over.homMk x

def mk (b : Œì ‚ü∂ B) (x : pullback b P.p ‚ü∂ X) : Œì ‚ü∂ P @ X :=
  (MvPoly.Equiv.mk (P := P.mvPoly) (Œì := Over.mk (terminal.from Œì))
    (Over.mk b) (by congr; apply terminal.hom_ext) (mkAux b x)).left

def mk' (b : Œì ‚ü∂ B) {pb f g} (H : IsPullback (P := pb) f g b P.p) (x : pb ‚ü∂ X) : Œì ‚ü∂ P @ X :=
  mk b (H.isoPullback.inv ‚â´ x)

theorem mk_eq_mk' (b : Œì ‚ü∂ B) (x : pullback b P.p ‚ü∂ X) :
    mk b x = mk' b (.of_hasPullback ..) x := by simp [mk']

@[simp]
lemma fst_mk (b : Œì ‚ü∂ B) (x : pullback b P.p ‚ü∂ X) :
    fst (mk b x) = b := by
  simp only [fst, mk, Over.homMk_eta]
  rw! (castMode := .all) [MvPoly.Equiv.fst_mk]
  simp [‚Üê heq_eq_eq]; rfl

@[simp]
lemma fst_mk' (b : Œì ‚ü∂ B) {pb f g} (H : IsPullback (P := pb) f g b P.p) (x : pb ‚ü∂ X) :
    fst (mk' b H x) = b := by
  simp [mk']

@[simp]
lemma mk'_comp_fstProj (b : Œì ‚ü∂ B) {pb f g} (H : IsPullback (P := pb) f g b P.p) (x : pb ‚ü∂ X) :
    mk' b H x ‚â´ P.fstProj X = b := by
  simp [‚Üê fst_eq]

theorem fst_comp_left (pair : Œì ‚ü∂ P @ X) {Œî} (f : Œî ‚ü∂ Œì) :
    fst (f ‚â´ pair) = f ‚â´ fst pair := by simp [fst_eq]

theorem fst_comp_right (pair : Œì ‚ü∂ P @ X) (f : X ‚ü∂ Y) :
    fst (pair ‚â´ P.functor.map f) = fst pair := by
  simp [fst_eq]

lemma snd'_eq (pair : Œì ‚ü∂ P @ X) {pb f g} (H : IsPullback (P := pb) f g (fst pair) P.p) :
    snd' pair H = pullback.lift (f ‚â´ pair) g (by simpa using H.w) ‚â´ sndProj P X := by
  rw [snd', snd_eq, ‚Üê Category.assoc]
  congr 1
  ext <;> simp

/-- Switch the selected pullback `pb` used in `UvPoly.Equiv.snd'` with a different pullback `pb'`. -/
lemma snd'_eq_snd' (pair : Œì ‚ü∂ P @ X) {pb f g} (H : IsPullback (P := pb) f g (fst pair) P.p)
    {pb' f' g'} (H' : IsPullback (P := pb') f' g' (fst pair) P.p) :
    snd' pair H = (H.isoIsPullback _ _ H').hom ‚â´ snd' pair H' := by
  simp [snd'_eq, ‚Üê Category.assoc]
  congr 2
  ext <;> simp

@[simp]
lemma snd_mk (b : Œì ‚ü∂ B) (x : pullback b P.p ‚ü∂ X) : snd (mk b x) =
    eqToHom (by simp) ‚â´ x := by
  have := MvPoly.Equiv.snd_mk (P := P.mvPoly) (Œì := Over.mk (terminal.from Œì))
    (Over.mk b) (by congr; apply terminal.hom_ext) (mkAux b x)
  convert congr_arg CommaMorphism.left this
  simp

@[simp]
lemma snd'_mk' (b : Œì ‚ü∂ B) {pb f g} (H : IsPullback (P := pb) f g b P.p) (x : pb ‚ü∂ X) :
    snd' (mk' b H x) (by rwa [fst_mk']) = x := by
  simp only [snd', mk', snd_mk]
  rw! [fst_mk]
  simp

@[simp]
lemma snd'_mk'' (b : Œì ‚ü∂ B) {pb f g} (H : IsPullback (P := pb) f g b P.p) (x : pb ‚ü∂ X)
   {pb' f' g'} (H' : IsPullback (P := pb') f' g' (fst (mk' b H x)) P.p := by exact H) :
    snd' (mk' b H x) H' = H.lift f' g' (by rw [fst_mk'] at H'; simp [H'.w]) ‚â´ x := by
  simp only [snd', mk', snd_mk]
  rw! [fst_mk]
  simp [‚Üê Category.assoc]
  congr 1
  apply H.hom_ext <;> simp


lemma snd_mk_heq (b : Œì ‚ü∂ B) (x : pullback b P.p ‚ü∂ X) :
    snd (mk b x) ‚âç x := by
  simp

theorem snd'_comp_left (pair : Œì ‚ü∂ P @ X)
    {pb f g} (H : IsPullback (P := pb) f g (fst pair) P.p)
    {Œî} (œÉ : Œî ‚ü∂ Œì)
    {pb' f' g'} (H' : IsPullback (P := pb') f' g' (œÉ ‚â´ fst pair) P.p) :
    snd' (œÉ ‚â´ pair) (by convert H'; rw [fst_comp_left]) =
    H.lift (f' ‚â´ œÉ) g' (by simp [H'.w]) ‚â´ snd' pair H := by
  simp only [snd'_eq, ‚Üê Category.assoc]
  congr 2
  ext
  ¬∑ simp
  ¬∑ simp

theorem snd'_comp_right (pair : Œì ‚ü∂ P @ X) (f : X ‚ü∂ Y)
    {pb f1 f2} (H : IsPullback (P := pb) f1 f2 (fst pair) P.p) :
    snd' (pair ‚â´ P.functor.map f) (by rwa [fst_comp_right]) =
    snd' pair H ‚â´ f := by
  simp only [snd'_eq, assoc]
  conv => right; rw [sndProj_comp, ‚Üê Category.assoc]
  congr 1
  ext <;> simp

theorem snd_comp_right (pair : Œì ‚ü∂ P @ X) (f : X ‚ü∂ Y) : snd (pair ‚â´ P.functor.map f) =
    eqToHom (by congr 1; apply fst_comp_right) ‚â´ snd pair ‚â´ f := by
  simp only [snd_eq, assoc, sndProj_comp]
  conv => right; simp only [‚Üê Category.assoc]
  congr 1
  have : fst (pair ‚â´ P.functor.map f) = fst pair := by simp [fst_eq]
  rw! [this]
  ext <;> simp

@[simp]
lemma eta (pair : Œì ‚ü∂ P @ X) :
    mk (fst pair) (snd pair) = pair := by
  have := MvPoly.Equiv.eta (P := P.mvPoly) (Œì := Over.mk (terminal.from Œì)) (homMk pair)
  exact congr_arg CommaMorphism.left this

@[simp]
lemma eta' (pair : Œì ‚ü∂ P @ X)
    {pb f1 f2} (H : IsPullback (P := pb) f1 f2 (fst pair) P.p) :
    mk' (fst pair) H (snd' pair H) = pair := by
  simp only [mk', snd']
  simp

lemma ext' {pair‚ÇÅ pair‚ÇÇ : Œì ‚ü∂ P @ X}
    {pb f g} (H : IsPullback (P := pb) f g (fst pair‚ÇÅ) P.p)
    (h1 : fst pair‚ÇÅ = fst pair‚ÇÇ)
    (h2 : snd' pair‚ÇÅ H = snd' pair‚ÇÇ (by rwa [h1] at H)) :
    pair‚ÇÅ = pair‚ÇÇ := by
  rw [‚Üê eta' pair‚ÇÅ H, ‚Üê eta' pair‚ÇÇ (by rwa [h1] at H), h2]
  rw! [h1]

/-- Switch the selected pullback `pb` used in `UvPoly.Equiv.mk'` with a different pullback `pb'`. -/
theorem mk'_eq_mk' (b : Œì ‚ü∂ B) {pb f g} (H : IsPullback (P := pb) f g b P.p) (x : pb ‚ü∂ X)
    {pb' f' g'} (H' : IsPullback (P := pb') f' g' b P.p) :
    mk' b H x = mk' b H' ((IsPullback.isoIsPullback _ _ H H').inv ‚â´ x) := by
  apply ext' (R := R) (f := f) (g := g) (by convert H; simp)
  ¬∑ have : ‚àÄ h, H'.lift f g h ‚â´ (IsPullback.isoIsPullback Œì E H H').inv = ùüô pb := by
      intro ; apply H.hom_ext <;> simp
    simp [‚Üê Category.assoc, this]
  ¬∑ simp

lemma mk'_comp_right (b : Œì ‚ü∂ B) {pb f1 f2} (H : IsPullback (P := pb) f1 f2 b P.p) (x : pb ‚ü∂ X)
    (f : X ‚ü∂ Y) : mk' b H x ‚â´ P.functor.map f = mk' b H (x ‚â´ f) := by
  refine .symm <| ext' (by rwa [fst_mk']) (by simp [fst_comp_right]) ?_
  rw [snd'_comp_right (H := by rwa [fst_mk'])]; simp

lemma mk_comp_right (b : Œì ‚ü∂ B) (x : pullback b P.p ‚ü∂ X) (f : X ‚ü∂ Y) :
    mk b x ‚â´ P.functor.map f = mk b (x ‚â´ f) := by
  simp [mk_eq_mk', mk'_comp_right]

theorem mk'_comp_left {Œî}
    (b : Œì ‚ü∂ B) {pb f g} (H : IsPullback f g b P.p) (x : pb ‚ü∂ X) (œÉ : Œî ‚ü∂ Œì)
    (œÉb) (eq : œÉ ‚â´ b = œÉb)
    {pb' f' g'} (H' : IsPullback (P := pb') f' g' œÉb P.p) :
    œÉ ‚â´ UvPoly.Equiv.mk' b H x = UvPoly.Equiv.mk' œÉb H'
    (H.lift (f' ‚â´ œÉ) g' (by simp [eq, H'.w]) ‚â´ x) := by
  apply ext' (f := f') (g := g') (H := by convert H'; simp [eq, fst_eq])
  ¬∑ rw [snd'_comp_left (H := by convert H; rw [fst_mk']) (H' := by convert H'; rw [‚Üê eq, fst_mk'])]
    simp
  ¬∑ simp [eq, fst_comp_left]

theorem mk_comp_left {Œî} (b : Œì ‚ü∂ B) (x : pullback b P.p ‚ü∂ X) (œÉ: Œî ‚ü∂ Œì) :
    œÉ ‚â´ UvPoly.Equiv.mk b x =
    UvPoly.Equiv.mk (œÉ ‚â´ b)
      (pullback.map _ _ _ _ œÉ (ùüô _) (ùüô _) (by simp) (by simp) ‚â´ x) := by
  simp only [mk_eq_mk']
  rw [mk'_comp_left (H := .of_hasPullback _ _) (H' := .of_hasPullback _ _) (eq := rfl)]
  congr 2; ext <;> simp

-- lemma mk'_comp_cartesianNatTrans_app {E' B' Œì X : C} {P' : UvPoly R E' B'}
--     (y : Œì ‚ü∂ B) (pb f g) (H : IsPullback (P := pb) f g y P.p.1)
--     (x : pb ‚ü∂ X) (e : E ‚ü∂ E') (b : B ‚ü∂ B')
--     (hp : IsPullback P.p.1 e b P'.p.1) :
--     Equiv.mk' y H x ‚â´ (P.cartesianNatTrans P' b e hp).app X =
--     Equiv.mk' P' X (y ‚â´ b) (H.paste_vert hp) x := by
--   have : fst P' X (Equiv.mk' P X y H x ‚â´ (P.cartesianNatTrans P' b e hp).app X) = y ‚â´ b := by
--     rw [fst_eq, Category.assoc, cartesianNatTrans_fstProj, ‚Üê Category.assoc, mk'_comp_fstProj]
--   refine ext' _ _ (this ‚ñ∏ H.paste_vert hp) (by simpa) ?_
--   simp; rw [snd'_eq]
--   have := snd'_mk' P X y H x
--   rw [snd'_eq, ‚Üê fan_snd_map' _ _ X hp] at this
--   refine .trans ?_ this
--   simp only [‚Üê Category.assoc]; congr 1; ext <;> simp

end Equiv

namespace compDomEquiv

variable {Œì E B E' B' : C} {P : UvPoly R E B} {P' : UvPoly R E' B'}

/-
```
   Œì
   |
   |triple
   V
 compDom
   |‚üç
   |   ‚üç
   |      ‚üç
   V         ‚Üò
   ‚Ä¢ -------> E
   |          |
   |   (pb)   |p
   |          |
   V          V
P @ B' -----> B
       fstProj
```
This produces a map `fst : Œì ‚ü∂ E`,
and a map `(triple ‚â´ P.comp P').p : Œì ‚ü∂ P @ B'`,
which we can further break up using `UvPoly.Equiv.fst` and `UvPoly.Equiv.snd`.
```
  dependent
B <---- pb ------> E
        |          |
        |          |p
        |          |
        V          V
        Œì -------> B
            base
```
-/
def fst (triple : Œì ‚ü∂ compDom P P') : Œì ‚ü∂ E :=
  triple ‚â´ pullback.fst _ _ ‚â´ pullback.snd _ _

@[simp]
abbrev base (triple : Œì ‚ü∂ compDom P P') : Œì ‚ü∂ B := Equiv.fst (triple ‚â´ (P.comp P').p)

theorem fst_comp_p (triple : Œì ‚ü∂ compDom P P') :
    fst triple ‚â´ P.p = base triple := by
  simp [fst, Equiv.fst_eq, pullback.condition]

abbrev dependent (triple : Œì ‚ü∂ compDom P P') {pb} (f : pb ‚ü∂ Œì) (g : pb ‚ü∂ E)
    (H : IsPullback f g (fst triple ‚â´ P.p) P.p) : pb ‚ü∂ B' :=
  Equiv.snd' (triple ‚â´ (P.comp P').p) (by convert H; simp only [fst_comp_p])

def snd (triple : Œì ‚ü∂ compDom P P') : Œì ‚ü∂ E' :=
  triple ‚â´ pullback.snd _ _

theorem snd_comp_p (triple : Œì ‚ü∂ compDom P P')
    {pb} (f : pb ‚ü∂ Œì) (g : pb ‚ü∂ E) (H : IsPullback f g (fst triple ‚â´ P.p) P.p) :
    snd triple ‚â´ P'.p =
    H.lift (ùüô Œì) (fst triple) (by simp) ‚â´ dependent triple f g H :=
  calc (triple ‚â´ pullback.snd _ _) ‚â´ P'.p
  _ = triple ‚â´ pullback.fst _ _ ‚â´ sndProj P B' := by
    simp [pullback.condition]
  _ = H.lift (ùüô Œì) (fst triple) (by simp) ‚â´ dependent triple f g H := by
    simp only [‚Üê assoc, dependent, comp_p, Equiv.snd'_eq]
    congr 1
    ext <;> simp [fst]

def mk (b : Œì ‚ü∂ B) (e : Œì ‚ü∂ E) (he : e ‚â´ P.p = b)
    {pb} (f : pb ‚ü∂ Œì) (g : pb ‚ü∂ E) (H : IsPullback f g b P.p)
    (b' : pb ‚ü∂ B') (e' : Œì ‚ü∂ E') (he' : e' ‚â´ P'.p = H.lift (ùüô Œì) e (by simp [he]) ‚â´ b') :
    Œì ‚ü∂ P.compDom P' :=
  pullback.lift (pullback.lift (Equiv.mk' b H b') e) e' (by
    have : b' = Equiv.snd' (Equiv.mk' b H b') (by convert H; simp) := by rw [Equiv.snd'_mk']
    conv => right; rw [he', this, Equiv.snd'_eq, ‚Üê Category.assoc]
    congr 1
    ext <;> simp )

@[simp]
lemma base_mk (b : Œì ‚ü∂ B) (e : Œì ‚ü∂ E) (he : e ‚â´ P.p = b)
    {pb} (f : pb ‚ü∂ Œì) (g : pb ‚ü∂ E) (H : IsPullback f g b P.p)
    (b' : pb ‚ü∂ B') (e' : Œì ‚ü∂ E') (he' : e' ‚â´ P'.p = H.lift (ùüô Œì) e (by simp [he]) ‚â´ b') :
  base (mk b e he f g H b' e' he') = b := by simp [mk]

@[simp]
lemma fst_mk (b : Œì ‚ü∂ B) (e : Œì ‚ü∂ E) (he : e ‚â´ P.p = b)
    {pb} (f : pb ‚ü∂ Œì) (g : pb ‚ü∂ E) (H : IsPullback f g b P.p)
    (b' : pb ‚ü∂ B') (e' : Œì ‚ü∂ E') (he' : e' ‚â´ P'.p = H.lift (ùüô Œì) e (by simp [he]) ‚â´ b') :
  fst (mk b e he f g H b' e' he') = e := by
  simp [mk, fst]

@[simp]
lemma dependent_mk (b : Œì ‚ü∂ B) (e : Œì ‚ü∂ E) (he : e ‚â´ P.p = b)
    {pb} (f : pb ‚ü∂ Œì) (g : pb ‚ü∂ E) (H : IsPullback f g b P.p)
    (b' : pb ‚ü∂ B') (e' : Œì ‚ü∂ E') (he' : e' ‚â´ P'.p = H.lift (ùüô Œì) e (by simp [he]) ‚â´ b')
    {pb'} (f' : pb' ‚ü∂ Œì) (g' : pb' ‚ü∂ E)
    (H' : IsPullback f' g' (fst (mk b e he f g H b' e' he') ‚â´ P.p) P.p) :
  dependent (mk b e he f g H b' e' he') f' g' H' = H.lift f' g' (by simp [‚Üê H'.w, he]) ‚â´ b' := by
  simp [mk, dependent]

@[simp]
lemma snd_mk (b : Œì ‚ü∂ B) (e : Œì ‚ü∂ E) (he : e ‚â´ P.p = b)
    {pb} (f : pb ‚ü∂ Œì) (g : pb ‚ü∂ E) (H : IsPullback f g b P.p)
    (b' : pb ‚ü∂ B') (e' : Œì ‚ü∂ E') (he' : e' ‚â´ P'.p = H.lift (ùüô Œì) e (by simp [he]) ‚â´ b') :
  snd (mk b e he f g H b' e' he') = e' := by
  simp [mk, snd]

@[simp]
lemma eta (triple : Œì ‚ü∂ compDom P P') {pb} (f : pb ‚ü∂ Œì) (g : pb ‚ü∂ E)
    (H : IsPullback f g (base triple) P.p) (b' : pb ‚ü∂ B')
    (hbase' : b' = Equiv.snd' (triple ‚â´ (P.comp P').p) H) :
    mk (base triple) (fst triple) (fst_comp_p ..) f g H b' (snd triple) (by
      simp only [snd, assoc, ‚Üê pullback.condition, base, comp_p]
      simp only [hbase', Equiv.snd'_eq, ‚Üê Category.assoc]
      congr 1
      ext <;> simp [fst]) = triple := by
  apply pullback.hom_ext
  ¬∑ ext
    ¬∑ simp [mk]
      conv => right; rw [‚Üê Equiv.eta'
        (triple ‚â´ pullback.fst (P.sndProj B') P'.p ‚â´ pullback.fst (P.fstProj B') P.p) H]
      congr
    ¬∑ simp [mk, fst]
  ¬∑ simp [mk, snd]

lemma ext (triple triple' : Œì ‚ü∂ compDom P P')
    (hfst : fst triple = fst triple')
    (hsnd : snd triple = snd triple')
    {pb} (f : pb ‚ü∂ Œì) (g : pb ‚ü∂ E)
    (H : IsPullback f g (fst triple ‚â´ P.p) P.p)
    (hd : dependent triple f g H = dependent triple' f g (by rwa [‚Üê hfst])) :
    triple = triple' := by
  rw [‚Üê eta triple f g (by convert H; simp [fst_comp_p]) (dependent triple f g H) rfl,
    ‚Üê eta triple' f g (by rwa [‚Üê fst_comp_p, ‚Üê hfst])
    (dependent triple' f g (by rwa [‚Üê hfst])) rfl]
  have : base triple = base triple' := by
    rw [‚Üê fst_comp_p, ‚Üê fst_comp_p, hfst]
  rw! [hsnd, hd, hfst, this]

lemma fst_comp {Œî} (œÉ : Œî ‚ü∂ Œì) (triple : Œì ‚ü∂ compDom P P') :
    fst (œÉ ‚â´ triple) = œÉ ‚â´ fst triple := by
  simp [fst]

lemma snd_comp {Œî} (œÉ : Œî ‚ü∂ Œì) (triple : Œì ‚ü∂ compDom P P') :
    snd (œÉ ‚â´ triple) = œÉ ‚â´ snd triple := by
  simp [snd]

lemma dependent_comp {Œî} (œÉ : Œî ‚ü∂ Œì) (triple : Œì ‚ü∂ compDom P P')
    {pb'} (f' : pb' ‚ü∂ Œì) (g' : pb' ‚ü∂ E) (H' : IsPullback f' g' (fst triple ‚â´ P.p) P.p)
    {pb} (f : pb ‚ü∂ Œî) (g : pb ‚ü∂ E) (H : IsPullback f g (fst (œÉ ‚â´ triple) ‚â´ P.p) P.p) :
    dependent (œÉ ‚â´ triple) f g H = H'.lift (f ‚â´ œÉ) g (by simp [‚Üê H.w, fst_comp]) ‚â´
    dependent triple f' g' H' := by
  simp only [dependent, comp_p, ‚Üê assoc, Equiv.snd'_eq]
  congr
  ext <;> simp

lemma comp_mk {Œî} (œÉ : Œî ‚ü∂ Œì) (b : Œì ‚ü∂ B) (e : Œì ‚ü∂ E) (he : e ‚â´ P.p = b)
    {pb'} (f' : pb' ‚ü∂ Œì) (g' : pb' ‚ü∂ E) (H' : IsPullback f' g' b P.p)
    {pb} (f : pb ‚ü∂ Œî) (g : pb ‚ü∂ E) (H : IsPullback f g (œÉ ‚â´ b) P.p)
    (b' : pb' ‚ü∂ B') (e' : Œì ‚ü∂ E') (he' : e' ‚â´ P'.p = H'.lift (ùüô Œì) e (by simp [he]) ‚â´ b') :
    œÉ ‚â´ mk b e he f' g' H' b' e' he' =
    mk (œÉ ‚â´ b) (œÉ ‚â´ e) (by simp [he]) f g H (H'.lift (f ‚â´ œÉ) g (by simp[‚Üê H.w]) ‚â´ b') (œÉ ‚â´ e')
    (by simp [he']; simp [‚Üê assoc]; congr 1; apply H'.hom_ext <;> simp) := by
  simp [mk]
  apply pullback.hom_ext
  ¬∑ apply pullback.hom_ext
    ¬∑ simp only [assoc, limit.lift_œÄ, PullbackCone.mk_pt, PullbackCone.mk_œÄ_app]
      rw [Equiv.mk'_comp_left]
      rfl
    ¬∑ simp
  ¬∑ simp

end compDomEquiv

instance preservesPullbacks (P : UvPoly R E B) {Pb X Y Z : C} (fst : Pb ‚ü∂ X) (snd : Pb ‚ü∂ Y)
    (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) (h: IsPullback fst snd f g) :
    IsPullback (P.functor.map fst) (P.functor.map snd) (P.functor.map f) (P.functor.map g) :=
  P.functor.map_isPullback h
